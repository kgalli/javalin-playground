plugins {
    id 'java'
    id 'nu.studer.jooq' version '3.0.3'
}

group 'kgalli.de'
version '1.0.0'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.javalin:javalin:2.6.0'
    compile "org.slf4j:slf4j-simple:1.7.25"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.8"

    // Guice
    compile group: 'com.google.inject', name: 'guice', version: '4.2.2'
    compile group: 'com.google.inject.extensions', name: 'guice-multibindings', version: '4.2.2'

    compile group: 'org.yaml', name: 'snakeyaml', version: '1.24'

    // database
    compile group: 'com.zaxxer', name: 'HikariCP', version: '3.3.1'
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.5'
    compile group: 'org.flywaydb', name: 'flyway-core', version: '5.2.4'
    // javax.annotation is necessary until jooq 3.12 is released
    // jooq version is set via jooq plugin see config below
    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    compile 'org.jooq:jooq'
    jooqRuntime group: 'org.postgresql', name: 'postgresql', version: '42.2.5'

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.4.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
    testCompile group: 'org.hamcrest', name: 'hamcrest', version: '2.1'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.198'
}

jar {
    from {
        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    manifest {
        attributes("Main-Class": "Main" )
    }
}

test {
    useJUnitPlatform()
}

jooq {

    version = '3.11.9'
    edition = 'OSS'
    //generate<db>JooqSchemaSource
    db(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = 'jdbc:postgresql://localhost:9999/kgalli'
            user = 'kgalli'
            password = 'kgalli'
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'public'
                forcedTypes {
                    forcedType {
                        name = 'varchar'
                        expression = '.*'
                        types = 'JSONB?'
                    }
                    forcedType {
                        name = 'varchar'
                        expression = '.*'
                        types = 'INET'
                    }
                }
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
            }
            target {
                packageName = 'de.kgalli.bookstore'
                directory = 'src/generated/jooq'
            }
        }
    }
}
project.tasks.getByName(project.sourceSets.main.compileJavaTaskName).dependsOn.remove("generateDbJooqSchemaSource")
