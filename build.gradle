plugins {
    id 'java'
    id 'nu.studer.jooq' version '3.0.3'
}

group 'kgalli.de'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
}

dependencies {
    compile 'io.javalin:javalin:2.6.0'
    compile "org.slf4j:slf4j-simple:1.7.25"
    compile "com.fasterxml.jackson.core:jackson-databind:2.9.8"

    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.5'
    compile group: 'org.flywaydb', name: 'flyway-core', version: '5.2.4'

    // javax.annotation is necessary until jooq 3.12 is released
    // jooq version is set via jooq plugin see config below
    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    compile 'org.jooq:jooq'

    jooqRuntime group: 'org.postgresql', name: 'postgresql', version: '42.2.5'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jooq {

    version = '3.11.9'
    edition = 'OSS'
    //generate<db>JooqSchemaSource
    db(sourceSets.main) {
        jdbc {
            driver = 'org.postgresql.Driver'
            url = 'jdbc:postgresql://localhost:9999/kgalli'
            user = 'kgalli'
            password = 'kgalli'
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            strategy {
                name = 'org.jooq.codegen.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.meta.postgres.PostgresDatabase'
                inputSchema = 'public'
                forcedTypes {
                    forcedType {
                        name = 'varchar'
                        expression = '.*'
                        types = 'JSONB?'
                    }
                    forcedType {
                        name = 'varchar'
                        expression = '.*'
                        types = 'INET'
                    }
                }
            }
            generate {
                relations = true
                deprecated = false
                records = true
                immutablePojos = true
                fluentSetters = true
            }
            target {
                packageName = 'de.kgalli.javalin-example'
                directory = 'src/main/java/db/jooq/autogenerated'
            }
        }
    }
}
